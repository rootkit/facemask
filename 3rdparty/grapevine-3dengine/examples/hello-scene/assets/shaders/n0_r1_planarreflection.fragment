// PRECISION
precision highp int;
precision highp float;

// IN from vertex shader
varying vec3 nNormal;
varying vec3 nEyeDir;
varying vec2 TextureCoord;

// IN from textures
uniform highp sampler2D ColorMap;
uniform highp samplerCube ReflectionMap;

// IN from renderer
uniform float Highlight;
uniform float Opacity;
uniform float SpecularFactor;
uniform float SpecularGlossiness;
uniform float ReflectionFactor;
uniform float StartOpacity;
uniform float YUsage;

// Lights
uniform highp vec3 nDiffuseLight;
uniform highp vec3 nSpecularLight;
uniform highp vec3 LightColorAmbient;
uniform highp vec3 LightColorDiffuse;
uniform highp vec3 LightColorSpecular;
const float pi = 3.14159;

void main (void)
{
    float yUsage = YUsage;
    float startOpacity = StartOpacity;

    float yCoordinate = clamp(TextureCoord.y / yUsage, 0.0, 1.0);
    vec2 tmpUV = vec2(TextureCoord.x, 1.0 - yCoordinate);

    // Samples at start of the shader to get the memory bus working while we do some calculations :)
    vec4 colorSample = texture2D(ColorMap, tmpUV);
    vec3 nCubeTexCoord = reflect(nEyeDir, nNormal);
    vec4 reflectionSample = textureCube(ReflectionMap, nCubeTexCoord);

    // Reflection for specular lightning
    vec3 nReflect = normalize(-reflect(nSpecularLight, nNormal));

    // Diffuse
    // Cosine angle between normal and light (both normalized so can be calculated like next)
    float cosineDiffuse = dot(nNormal, nDiffuseLight);
    float Diffuse = (pi - clamp(acos(cosineDiffuse), 0.0, pi)) / pi;

    // Specular
    // Cosine angle between normal and reflection (both normalized so can be calculated like next)
    float cosineSpecular = dot(nReflect, nNormal);
    float Specular = ((pi - clamp(acos(cosineSpecular), 0.0, pi))) / pi;
    Specular = SpecularFactor * pow(Specular, SpecularGlossiness);

    // Color - ambient (0.5, 0.5, 0.5) + diffuse 0.5 (white light) + specular 0.5 (white light)
    vec3 sample = 0.5 * colorSample.rgb;
    vec3 color = (LightColorAmbient * sample) + (LightColorDiffuse * Diffuse * sample) + (LightColorSpecular * Specular * vec3(0.5, 0.5, 0.5));

    // Color - reflection
    color = ((1.0 - ReflectionFactor) * color) + (ReflectionFactor * reflectionSample.rgb);

    // Color clamp
    clamp(color, vec3(0.0, 0.0, 0.0), vec3(1.0, 1.0, 1.0));

    // Color - glow
    vec3 fat = vec3(1.0, 1.0, 1.0) - color;
    color = color + Highlight * fat;

    // Color - planar reflection fade

    // Color - final
    float fakeFade = startOpacity * yCoordinate;
    gl_FragColor = vec4(color, fakeFade * Opacity * colorSample.a);
}
